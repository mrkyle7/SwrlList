apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")

def keystoreProperties = new Properties()

try {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} catch (Exception ignored){}


android {
    signingConfigs {
        config {
            keyAlias System.getenv('KEYALIAS') ?: 'kjhandroid'
            keyPassword System.getenv('KEYPASSWORD') ?: keystoreProperties['KEYPASSWORD']
            storeFile file(System.getenv('KEYFILE') ?: '/Users/kyle/androidkey-swrl.jks')
            storePassword System.getenv('STOREPASSWORD') ?: keystoreProperties['STOREPASSWORD']
        }
    }
    compileSdkVersion 22
    buildToolsVersion "24.0.2"
    defaultConfig {
        applicationId "co.swrl.list"
        minSdkVersion 19
        targetSdkVersion 22
        versionCode 13
        versionName "7.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile('com.android.support.test.espresso:espresso-intents:2.2.2')
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:design:22.2.1'
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:2.2.8"
    testCompile 'com.squareup.okhttp3:mockwebserver:3.6.0'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    compile 'com.getbase:floatingactionbutton:1.10.1'
    androidTestCompile ('com.getbase:floatingactionbutton:1.10.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

apply plugin: 'devicefarm'

devicefarm {
    projectName System.getenv('DEVICEFARMPROJECTNAME') ?: 'Swrl List'
    devicePool System.getenv('DEVICEFARMDEVICEPOOL') ?: 'Android Phone'
    authentication {
        accessKey System.getenv('DEVICEFARMACCESSKEY') ?: 'none'
        secretKey System.getenv('DEVICEFARMSECRETKEY') ?: 'none'
    }
}

apply plugin: 'com.github.triplet.play'

play {
    jsonFile = file('play-store-keys.json')
    uploadImages = true
}