apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")

def keystoreProperties = new Properties()

try {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} catch (Exception ignored) {
}


android {
    signingConfigs {
        config {
            keyAlias System.getenv('KEYALIAS') ?: 'kjhandroid'
            keyPassword System.getenv('KEYPASSWORD') ?: keystoreProperties['KEYPASSWORD']
            storeFile file(System.getenv('KEYFILE') ?: '/Users/kyle/androidkey-swrl.jks')
            storePassword System.getenv('STOREPASSWORD') ?: keystoreProperties['STOREPASSWORD']
        }
    }
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        applicationId "co.swrl.list"
        minSdkVersion 22
        targetSdkVersion 25
        versionCode 74
        versionName "22"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile('com.android.support.test.espresso:espresso-intents:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2', {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    })

    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:support-annotations:25.3.1'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.getbase:floatingactionbutton:1.10.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.2.8'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.6.0'
    androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.6.0'
}

apply plugin: 'devicefarm'

devicefarm {
    projectName System.getenv('DEVICEFARMPROJECTNAME') ?: 'Swrl List'
    devicePool System.getenv('DEVICEFARMDEVICEPOOL') ?: 'Android Phone'
    authentication {
        accessKey System.getenv('DEVICEFARMACCESSKEY') ?: 'none'
        secretKey System.getenv('DEVICEFARMSECRETKEY') ?: 'none'
    }
}

apply plugin: 'com.github.triplet.play'


play {
    jsonFile = file('play-store-keys.json')
    uploadImages = true
}